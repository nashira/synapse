apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    def dateCode = (System.currentTimeMillis() / 10_000).toInteger()
    if (dateCode > 173571120) {
        throw new RuntimeException("bad version code '$dateCode', check clock")
    }
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.rthqks.synapse"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode dateCode
        versionName "0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    flavorDimensions "product"
    productFlavors {
        synapse {
            applicationIdSuffix ".next"
            dimension "product"
            versionNameSuffix "-synapse"
        }
        polish {
            dimension "product"
            versionNameSuffix "-polish"
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    kotlinOptions { jvmTarget = "1.8" }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildToolsVersion = '28.0.3'
    playConfigs {
        polishRelease {
            enabled = true
            userFraction = 1.0
        }
    }
}

play {
    serviceAccountCredentials = file("../play-api.json")
    enabled = false
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // support lib
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.exifinterface:exifinterface:1.1.0"

    // firebase
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // lifecycle
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // dagger
    implementation 'com.google.dagger:dagger:2.26'
    implementation 'com.google.dagger:dagger-android-support:2.26'
    kapt 'com.google.dagger:dagger-compiler:2.26'
    kapt 'com.google.dagger:dagger-android-processor:2.26'

    // room
    def room_version = "2.2.4"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // stetho
    debugImplementation 'com.facebook.stetho:stetho:1.5.1'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
