//apply plugin: 'com.github.triplet.play'
//apply plugin: 'com.google.firebase.crashlytics'
//apply plugin: 'com.google.gms.google-services'
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "org.jetbrains.kotlin.kapt"
//    id 'dagger.hilt.android.plugin'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def dateCode = (System.currentTimeMillis() / 10_000).toInteger()
//if (dateCode > 173571120) {
//    throw new RuntimeException("bad version code '$dateCode', check clock")
//}
def num = (dateCode / 360).toInteger() % 10_000

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.rthqks.synapse"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1
        versionName "dev"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.incremental":"true"]
            }
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.each { output ->
                output.versionCodeOverride = dateCode
                output.versionNameOverride = "0.5.$num"
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            ext.alwaysUpdateBuildId = false
        }
    }

    flavorDimensions "stage"
    productFlavors {
        dev {
            dimension "stage"
            resConfigs "en", "xxhdpi"
        }

        prod {
            dimension "stage"
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        viewBinding true
    }

//    playConfigs {
//        release {
//            enabled.set(true)
//            userFraction.set(1.0D)
//        }
//    }
}

//play {
//    serviceAccountCredentials = file("../play-api.json")
//    enabled = false
//}

dependencies {
    implementation project(path: ':flow')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation 'androidx.core:core-ktx:1.7.0'

    // support lib
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.fragment:fragment-ktx:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // firebase
    implementation 'com.google.firebase:firebase-analytics:20.0.2'

    // lifecycle
    def lifecycle_version = "2.4.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // dagger
    implementation 'com.google.dagger:dagger:2.36'
    implementation 'com.google.dagger:dagger-android-support:2.28.3'
    kapt 'com.google.dagger:dagger-compiler:2.36'
    kapt 'com.google.dagger:dagger-android-processor:2.28.3'

    // room
    def room_version = "2.4.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // gson
    implementation 'com.google.code.gson:gson:2.8.6'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.robolectric:robolectric:4.3.1'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

