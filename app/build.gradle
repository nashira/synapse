apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def dateCode = (System.currentTimeMillis() / 10_000).toInteger()
if (dateCode > 173571120) {
    throw new RuntimeException("bad version code '$dateCode', check clock")
}
def num = (dateCode / 360).toInteger() % 10_000

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.rthqks.synapse"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "dev"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.incremental":"true"]
            }
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.each { output ->
                output.versionCodeOverride = dateCode
                output.versionNameOverride = "0.5.$num"
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            ext.alwaysUpdateBuildId = false
        }
    }

    flavorDimensions "stage"
    productFlavors {
        dev {
            dimension "stage"
            resConfigs "en", "xxhdpi"
        }

        prod {
            dimension "stage"
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    kotlinOptions { jvmTarget = "1.8" }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests {
            includeAndroidResources = true
        }
    }

    playConfigs {
        release {
            enabled = true
            userFraction = 1.0
        }
    }
}

play {
    serviceAccountCredentials = file("../play-api.json")
    enabled = false
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // support lib
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // firebase
    implementation 'com.google.firebase:firebase-analytics:17.4.4'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // lifecycle
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // dagger
    implementation 'com.google.dagger:dagger:2.26'
    implementation 'com.google.dagger:dagger-android-support:2.26'
    kapt 'com.google.dagger:dagger-compiler:2.26'
    kapt 'com.google.dagger:dagger-android-processor:2.26'

    // room
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // stetho
    debugImplementation 'com.facebook.stetho:stetho:1.5.1'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    testImplementation 'junit:junit:4.13'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.robolectric:robolectric:4.3'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
